#version 450

#include "brickDebug_bindings.h"

layout(constant_id = VOXEL_GRID_WIDTH_CONST_ID) const uint cVoxelGridWidth = 128;
layout(constant_id = VOXEL_GRID_HEIGHT_CONST_ID) const uint cVoxelGridHeight = 64;
layout(constant_id = VOXEL_GRID_DEPTH_CONST_ID) const uint cVoxelGridDepth = 128;
layout(constant_id = WIDTH_CONST_ID) const uint cWidth = 1600;
layout(constant_id = HEIGHT_CONST_ID) const uint cHeight = 900;
layout(constant_id = TEXEL_WIDTH_CONST_ID) const float cTexelWidth = 1.0 / 1600.0;
layout(constant_id = TEXEL_HEIGHT_CONST_ID) const float cTexelHeight = 1.0 / 900;

layout(set = BRICK_PTR_IMAGE_SET, binding = BRICK_PTR_IMAGE_BINDING) uniform usampler3D uBrickPtrImage;
layout(set = RESULT_IMAGE_SET, binding = RESULT_IMAGE_BINDING, rgba16f) uniform writeonly image2D uResultImage;
layout(set = BIN_VIS_IMAGE_BUFFER_SET, binding = BIN_VIS_IMAGE_BUFFER_BINDING) uniform usamplerBuffer uBinVisImageBuffer;
layout(set = COLOR_IMAGE_BUFFER_SET, binding = COLOR_IMAGE_BUFFER_BINDING) uniform samplerBuffer uColorImageBuffer;

layout(push_constant) uniform PUSH_CONSTS 
{
	PushConsts uPushConsts;
};

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

const ivec3 gridSize = ivec3(cVoxelGridWidth, cVoxelGridHeight, cVoxelGridDepth);

bool fetchVisibility(ivec3 coord, out vec3 color)
{
	const vec3 brickGridSize = vec3(128, 64, 128);
	const vec3 invBrickGridSize = 1.0 / brickGridSize;
	const vec3 invGridSize = 1.0 / vec3(gridSize);
	
	ivec3 brickCoord = ivec3(fract((coord / 16) * invBrickGridSize) * brickGridSize);
	uint brickPtr = texelFetch(uBrickPtrImage, brickCoord, 0).x;
	
	if (brickPtr != 0)
	{
		ivec3 cubeCoord = coord / 4;
		ivec3 localCubeCoord = ivec3(fract(cubeCoord / 4.0) * 4.0);
		uint cubeIdx = localCubeCoord.x + localCubeCoord.z * 4 + localCubeCoord.y * 16;
		ivec3 bitCoord = ivec3(fract(coord / 4.0) * 4.0);
		uint bitIdx = bitCoord.x + bitCoord.z * 4 + bitCoord.y * 16;
		bool upper = bitIdx > 31;
		bitIdx = upper ? bitIdx - 32 : bitIdx;
		uint binVis = texelFetch(uBinVisImageBuffer, int((brickPtr - 1) * 128 + cubeIdx * 2 + (upper ? 1 : 0))).x;
		if ((binVis & (1u << bitIdx)) != 0)
		{
			color = texelFetch(uColorImageBuffer, int((brickPtr - 1) * 64 + cubeIdx)).rgb;
			return true;
		}
		return false;
	}
	else
	{
		return false;
	}
}

vec4 raymarch(vec3 worldSpacePos, vec3 dir)
{
	// world space offset of grid (grid is centered on camera)
	ivec3 offset = ivec3(round(vec3(uPushConsts.cameraPos) * uPushConsts.voxelScale)) - (gridSize / 2);
	vec3 p0 = worldSpacePos.xyz * uPushConsts.voxelScale + 0.5;
	
	// grid space
	vec3 gridCoord = floor(p0);
	const vec3 gridStep = sign(dir);
	const vec3 corner = max(gridStep, vec3(0.0));
	const vec3 invGridSize = 1.0 / vec3(gridSize);
	
	// ray space
	const vec3 inv = 1.0 / dir;
	vec3 ratio = (gridCoord + corner - p0) * inv;
	const vec3 ratioStep = gridStep * inv;
	
	// dda
	while (true) 
	{
		if (any(lessThan(gridCoord, offset)) || any(greaterThanEqual(gridCoord, gridSize + offset)))
		{
			return vec4(0.0);
		}
		
		vec3 color;
		if (fetchVisibility(ivec3(gridCoord), color))
		{
			return vec4(color, 1.0);
		}
	
		const ivec3 mask = ivec3(lessThanEqual(ratio.xyz, min(ratio.yzx, ratio.zxy)));
		gridCoord += gridStep  * mask;		
		ratio += ratioStep * mask;
	}
}

void main() 
{
	if (gl_GlobalInvocationID.x >= cWidth || gl_GlobalInvocationID.y >= cHeight)
	{
		return;
	}
	
	vec4 worldSpacePos = uPushConsts.invViewProjection * vec4(vec2(gl_GlobalInvocationID.xy) * vec2(cTexelWidth, cTexelHeight) * 2.0 - 1.0, 1.0, 1.0);
	worldSpacePos.xyz /= worldSpacePos.w;
	
	vec4 direction4 = uPushConsts.invViewProjection * vec4(vec2(gl_GlobalInvocationID.xy) * vec2(cTexelWidth, cTexelHeight) * 2.0 - 1.0, 0.0, 1.0);
	vec3 direction = normalize(direction4.xyz / direction4.w - worldSpacePos.xyz);

	vec4 c = raymarch(worldSpacePos.xyz, direction);
	
	if (c.a > 0.0)
	{
		imageStore(uResultImage, ivec2(gl_GlobalInvocationID.xy), c);
	}
}